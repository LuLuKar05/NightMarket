pragma language_version 0.18.0;

import CompactStandardLibrary;

export ledger position_commitments: HistoricMerkleTree<10, Bytes<32>>;
export ledger position_nullifiers: Set<Bytes<32>>;

witness findAuthPath(commitment: Bytes<32>): MerkleTreePath<10, Bytes<32>>;
witness secretKey(): Bytes<32>;

export circuit add_position(commitment: Bytes<32>): [] {
  position_commitments.insert(disclose(commitment));
}

export circuit spend_position(): [] {
  const sk = secretKey();
  const authPath = disclose(findAuthPath(publicKey(sk)));
  assert(position_commitments.checkRoot(merkleTreePathRoot<10, Bytes<32>>(authPath)), "not authorized");
  const nul = disclose(nullifier(sk));
  assert(!position_nullifiers.member(nul), "already spent");
  position_nullifiers.insert(nul);
}

circuit publicKey(sk: Bytes<32>): Bytes<32> {
  return persistentHash<Vector<2, Bytes<32>>>([pad(32, "commitment-domain"), sk]);
}

circuit nullifier(sk: Bytes<32>): Bytes<32> {
  return persistentHash<Vector<2, Bytes<32>>>([pad(32, "nullifier-domain"), sk]);
}